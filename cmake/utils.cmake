# Get easylocal version from include/version.hh and put it in EASYLOCAL_VERSION
function(easylocal_extract_version EL_SOURCE_DIR)
    file(READ "${EL_SOURCE_DIR}/include/easylocal/version.hh" file_contents)
    string(REGEX MATCH "EASYLOCAL_VER_MAJOR ([0-9]+)" _ "${file_contents}")
    if(NOT CMAKE_MATCH_COUNT EQUAL 1)
        message(FATAL_ERROR "Could not extract major version number from version.hh")
    endif()
    set(ver_major ${CMAKE_MATCH_1})

    string(REGEX MATCH "EASYLOCAL_VER_MINOR ([0-9]+)" _ "${file_contents}")
    if(NOT CMAKE_MATCH_COUNT EQUAL 1)
        message(FATAL_ERROR "Could not extract minor version number from version.hh")
    endif()

    set(ver_minor ${CMAKE_MATCH_1})
    string(REGEX MATCH "EASYLOCAL_VER_PATCH ([0-9]+)" _ "${file_contents}")
    if(NOT CMAKE_MATCH_COUNT EQUAL 1)
        message(FATAL_ERROR "Could not extract patch version number from version.hh")
    endif()
    set(ver_patch ${CMAKE_MATCH_1})

    set(EASYLOCAL_VERSION_MAJOR ${ver_major} PARENT_SCOPE)
    set(EASYLOCAL_VERSION_MINOR ${ver_minor} PARENT_SCOPE)
    set(EASYLOCAL_VERSION_PATCH ${ver_patch} PARENT_SCOPE)
    set(EASYLOCAL_VERSION "${ver_major}.${ver_minor}.${ver_patch}" PARENT_SCOPE)
endfunction()